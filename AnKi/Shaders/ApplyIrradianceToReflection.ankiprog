// Copyright (C) 2009-2023, Panagiotis Christopoulos Charitos and contributors.
// All rights reserved.
// Code licensed under the BSD License.
// http://www.anki3d.org/LICENSE

#pragma anki start comp
#include <AnKi/Shaders/PackFunctions.hlsl>
#include <AnKi/Shaders/LightFunctions.hlsl>

[[vk::binding(0)]] SamplerState g_nearestAnyClampSampler;
[[vk::binding(1)]] Texture2D g_gbufferTex[3u];
[[vk::binding(2)]] StructuredBuffer<RVec4> g_irradianceDice;
[[vk::binding(3)]] RWTexture2D<RVec4> g_cubeTex[6u];

[numthreads(8, 8, 6)] void main(UVec3 svDispatchThreadId : SV_DISPATCHTHREADID,
								UVec3 svGroupThreadId : SV_GROUPTHREADID)
{
	UVec2 cubeSizeu;
	g_cubeTex[0].GetDimensions(cubeSizeu.x, cubeSizeu.y);
	const Vec2 cubeSize = Vec2(cubeSizeu);
	const UVec2 dispatchThreadId = min(svDispatchThreadId.xy, cubeSizeu - 1u);

	const U32 faceIdx = svGroupThreadId.z;

	// Compute the UVs to read the gbuffer from
	Vec2 sampleUv = (Vec2(dispatchThreadId) + 0.5) / Vec2(cubeSize);
	sampleUv.x *= (1.0 / 6.0);
	sampleUv.x += (1.0 / 6.0) * F32(faceIdx);

	// Read the gbuffer
	GbufferInfo gbuffer = (GbufferInfo)0;
	unpackGBufferNoVelocity(g_gbufferTex[0u].SampleLevel(g_nearestAnyClampSampler, sampleUv, 0.0),
							g_gbufferTex[1u].SampleLevel(g_nearestAnyClampSampler, sampleUv, 0.0),
							g_gbufferTex[2u].SampleLevel(g_nearestAnyClampSampler, sampleUv, 0.0), gbuffer);

	// Sample
	const RVec3 irradiance = sampleAmbientDice(g_irradianceDice[0u].xyz, g_irradianceDice[1u].xyz,
											   g_irradianceDice[2u].xyz, g_irradianceDice[3u].xyz,
											   g_irradianceDice[4u].xyz, g_irradianceDice[5u].xyz, gbuffer.m_normal);

	// Compute the indirect term
	const RVec3 indirect = gbuffer.m_diffuse * irradiance;

	// Read the prev color and apply indirect
	const RVec3 prevColor = g_cubeTex[faceIdx][dispatchThreadId].xyz;
	const RVec3 prevColorWithIndirectDiffuse = prevColor + gbuffer.m_diffuse * indirect;

	// Barrier just in case
	GroupMemoryBarrierWithGroupSync();

	// Write it back
	g_cubeTex[faceIdx][dispatchThreadId] = RVec4(prevColorWithIndirectDiffuse, 0.0);
}
#pragma anki end
