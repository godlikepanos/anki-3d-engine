// Copyright (C) 2009-2022, Panagiotis Christopoulos Charitos and contributors.
// All rights reserved.
// Code licensed under the BSD License.
// http://www.anki3d.org/LICENSE

// A slow compute program to clear an image with a contant color

#pragma anki hlsl

#pragma anki mutator TEXTURE_DIMENSIONS 2 3
#pragma anki mutator COMPONENT_TYPE 0 1 // 0 is float, 1 is uint

#pragma anki start comp
#include <AnKi/Shaders/Common.hlsl>

struct Uniforms
{
#if COMPONENT_TYPE == 0
	Vec4 m_clearColor;
#else
	UVec4 m_clearColor;
#endif
};

[[vk::push_constant]] ConstantBuffer<Uniforms> g_pc;

#if TEXTURE_DIMENSIONS == 2
#	if COMPONENT_TYPE == 0
[[vk::binding(0)]] RWTexture2D<Vec4> g_outUav;
#	else
[[vk::binding(0)]] RWTexture2D<UVec4> g_outUav;
#	endif
#else
#	if COMPONENT_TYPE == 0
[[vk::binding(0)]] RWTexture3D<Vec4> g_outUav;
#	else
[[vk::binding(0)]] RWTexture3D<UVec4> g_outUav;
#	endif
#endif

[numthreads(8, 8, (TEXTURE_DIMENSIONS == 2) ? 1 : 8)] void main(UVec3 svDispatchThreadId : SV_DISPATCHTHREADID)
{
#if TEXTURE_DIMENSIONS == 2
	UVec2 texSize;
	g_outUav.GetDimensions(texSize.x, texSize.y);

	if(svDispatchThreadId.x >= texSize.x || svDispatchThreadId.y >= texSize.y)
	{
		return;
	}

	g_outUav[svDispatchThreadId.xy] = g_pc.m_clearColor;
#else
	UVec3 texSize;
	g_outUav.GetDimensions(texSize.x, texSize.y, texSize.z);

	if(svDispatchThreadId.x >= texSize.x || svDispatchThreadId.y >= texSize.y || svDispatchThreadId.z >= texSize.z)
	{
		return;
	}

	g_outUav[svDispatchThreadId] = g_pc.m_clearColor;
#endif
}

#pragma anki end
