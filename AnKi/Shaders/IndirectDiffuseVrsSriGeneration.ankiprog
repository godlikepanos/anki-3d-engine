// Copyright (C) 2009-2022, Panagiotis Christopoulos Charitos and contributors.
// All rights reserved.
// Code licensed under the BSD License.
// http://www.anki3d.org/LICENSE

#pragma anki hlsl

#pragma anki mutator SRI_TEXEL_DIMENSION 8 16
#pragma anki mutator SHARED_MEMORY 0 1
#pragma anki mutator LIMIT_RATE_TO_2X2 0 1

#pragma anki start comp

#include <AnKi/Shaders/Functions.hlsl>

[[vk::binding(0)]] Texture2D g_inputTex;
[[vk::binding(1)]] SamplerState g_nearestClampSampler;

#if SRI_TEXEL_DIMENSION == 8
#	define REGION_SIZE_X 2
#	define REGION_SIZE_Y 2
#else
#	define REGION_SIZE_X 2
#	define REGION_SIZE_Y 4
#endif

#define THREAD_GROUP_SIZE_X (SRI_TEXEL_DIMENSION / REGION_SIZE_X)
#define THREAD_GROUP_SIZE_Y (SRI_TEXEL_DIMENSION / REGION_SIZE_Y)

[[vk::binding(2)]] RWTexture2D<UVec4> g_sriImg;

struct Uniforms
{
	Vec2 m_oneOverViewportSize;
	F32 m_thresholdMeters;
	F32 m_padding0;
	Mat4 m_invertedProjectionJitter;
};

[[vk::push_constant]] ConstantBuffer<Uniforms> g_unis;

#if SHARED_MEMORY
// Ideally, we'd be able to calculate the min/max/average using subgroup operations, but there's no guarantee
// subgroupSize is large enough so we need shared memory as a fallback. We need gl_NumSubgroups entries, but it is not a
// constant, so estimate it assuming a subgroupSize of at least 8.
constexpr U32 kSharedMemoryEntries = THREAD_GROUP_SIZE_X * THREAD_GROUP_SIZE_Y / 8u;
groupshared Vec2 s_maxDerivative[kSharedMemoryEntries];
groupshared U32 s_waveIndexInsideThreadGroup;
#endif

F32 sampleViewPositionZ(Vec2 uv, I32 offsetX, I32 offsetY)
{
	uv += Vec2(offsetX, offsetY) * g_unis.m_oneOverViewportSize;
	const Vec2 ndc = uvToNdc(uv);
	const F32 depth = g_inputTex.SampleLevel(g_nearestClampSampler, uv, 0.0).x;

	const Vec4 v4 = mul(g_unis.m_invertedProjectionJitter, Vec4(ndc, depth, 1.0));
	return v4.z / v4.w;
}

[numthreads(THREAD_GROUP_SIZE_X, THREAD_GROUP_SIZE_Y, 1)] void
main(UVec3 svDispatchThreadId : SV_DISPATCHTHREADID, U32 svGroupIndex : SV_GROUPINDEX, UVec3 svGroupId : SV_GROUPID)
{
#if SHARED_MEMORY
	U32 wavesPerThreadGroup;
	U32 waveIndexInsideThreadGroup;
	ANKI_COMPUTE_WAVE_INDEX_INSIDE_THREADGROUP(svGroupIndex, s_waveIndexInsideThreadGroup, waveIndexInsideThreadGroup,
											   wavesPerThreadGroup);
#endif

	const Vec2 uv =
		(Vec2(svDispatchThreadId.xy) * Vec2(REGION_SIZE_X, REGION_SIZE_Y) + 0.5) * g_unis.m_oneOverViewportSize;

#if SRI_TEXEL_DIMENSION == 8
	// Get positions
	// l0.z  l0.w
	// l0.x  l0.y
	Vec4 l0;
	l0.x = sampleViewPositionZ(uv, 0, 0);
	l0.y = sampleViewPositionZ(uv, 1, 0);
	l0.z = sampleViewPositionZ(uv, 0, 1);
	l0.w = sampleViewPositionZ(uv, 1, 1);

	// Calculate derivatives.
	Vec2 a = Vec2(l0.y, l0.z);
	Vec2 b = Vec2(l0.x, l0.w);
	const Vec2 dx = abs(a - b);

	a = Vec2(l0.z, l0.w);
	b = Vec2(l0.x, l0.y);
	const Vec2 dy = abs(a - b);

	F32 maxDerivativeX = max(dx.x, dx.y);
	F32 maxDerivativeY = max(dy.x, dy.y);
#else
	// Get positions
	// l1.z  l1.w
	// l1.x  l1.y
	// l0.z  l0.w
	// l0.x  l0.y
	Vec4 l0;
	l0.x = sampleViewPositionZ(uv, 0, 0);
	l0.y = sampleViewPositionZ(uv, 1, 0);
	l0.z = sampleViewPositionZ(uv, 0, 1);
	l0.w = sampleViewPositionZ(uv, 1, 1);

	Vec4 l1;
	l1.x = sampleViewPositionZ(uv, 0, 2);
	l1.y = sampleViewPositionZ(uv, 1, 2);
	l1.z = sampleViewPositionZ(uv, 0, 3);
	l1.w = sampleViewPositionZ(uv, 1, 3);

	// Calculate derivatives.
	Vec4 a = Vec4(l0.y, l0.z, l1.y, l1.z);
	Vec4 b = Vec4(l0.x, l0.w, l1.x, l1.w);
	const Vec4 dx = abs(a - b);

	a = Vec4(l0.z, l0.w, l1.z, l1.y);
	b = Vec4(l0.x, l0.y, l1.x, l1.w);
	const Vec4 dy = abs(a - b);

	F32 maxDerivativeX = max(max(dx.x, dx.y), max(dx.z, dx.w));
	F32 maxDerivativeY = max(max(dy.x, dy.y), max(dy.z, dy.w));
#endif

	maxDerivativeX = WaveActiveMax(maxDerivativeX);
	maxDerivativeY = WaveActiveMax(maxDerivativeY);

#if SHARED_MEMORY
	// Store results in shared memory.
	[branch] if(WaveIsFirstLane())
	{
		s_maxDerivative[waveIndexInsideThreadGroup] = Vec2(maxDerivativeX, maxDerivativeY);
	}

	GroupMemoryBarrierWithGroupSync();
#endif

	// Write the result
	[branch] if(svGroupIndex == 0u)
	{
		// Get max across all subgroups.
#if SHARED_MEMORY
		Vec2 maxDerivative = s_maxDerivative[0];

		for(U32 i = 1u; i < wavesPerThreadGroup; ++i)
		{
			maxDerivative = max(maxDerivative, s_maxDerivative[i]);
		}
#else
		const Vec2 maxDerivative = Vec2(maxDerivativeX, maxDerivativeY);
#endif

		// Determine shading rate.
		const F32 threshold1 = g_unis.m_thresholdMeters;
		const F32 threshold2 = threshold1 * 0.4;

		UVec2 rate;
		rate.x = (maxDerivative.x > threshold1) ? 1u : ((maxDerivative.x > threshold2) ? 2u : 4u);
		rate.y = (maxDerivative.y > threshold1) ? 1u : ((maxDerivative.y > threshold2) ? 2u : 4u);

#if LIMIT_RATE_TO_2X2
		rate = min(rate, UVec2(2u, 2u));
#endif

		const UVec2 outTexelCoord = svGroupId.xy;
		g_sriImg[outTexelCoord] = UVec4(encodeVrsRate(rate), 0, 0, 0);
	}
}

#pragma anki end
