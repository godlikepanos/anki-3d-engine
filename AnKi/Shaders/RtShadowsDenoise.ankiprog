// Copyright (C) 2009-2022, Panagiotis Christopoulos Charitos and contributors.
// All rights reserved.
// Code licensed under the BSD License.
// http://www.anki3d.org/LICENSE

#pragma anki hlsl

#pragma anki mutator BLUR_ORIENTATION 0 1

#pragma anki start comp

#include <AnKi/Shaders/BilateralFilter.hlsl>
#include <AnKi/Shaders/PackFunctions.hlsl>
#include <AnKi/Shaders/RtShadows.hlsl>
#include <AnKi/Shaders/Functions.hlsl>

ANKI_SPECIALIZATION_CONSTANT_UVEC2(kOutImageSize, 0u);
ANKI_SPECIALIZATION_CONSTANT_U32(kMinSampleCount, 2u);
ANKI_SPECIALIZATION_CONSTANT_U32(kMaxSampleCount, 3u);

[[vk::binding(0)]] SamplerState g_nearestAnyClampSampler;
[[vk::binding(1)]] SamplerState g_linearAnyClampSampler;
[[vk::binding(2)]] Texture2D<UVec4> g_inTex;
[[vk::binding(3)]] Texture2D g_depthTex;
[[vk::binding(4)]] Texture2D g_gbuffer2Tex;
[[vk::binding(5)]] Texture2D g_momentsTex;
[[vk::binding(6)]] Texture2D g_historyLengthTex;
[[vk::binding(7)]] RWTexture2D<UVec4> g_outImg;

[[vk::push_constant]] ConstantBuffer<RtShadowsDenoiseUniforms> g_unis;

Vec3 unproject(Vec2 ndc, F32 depth)
{
	const Vec4 worldPos4 = mul(g_unis.invViewProjMat, Vec4(ndc, depth, 1.0));
	const Vec3 worldPos = worldPos4.xyz / worldPos4.w;
	return worldPos;
}

F32 computeVarianceCenter(Vec2 uv)
{
	const F32 kernel[2][2] = {{1.0 / 4.0, 1.0 / 8.0}, {1.0 / 8.0, 1.0 / 16.0}};
	const I32 radius = 1;
	Vec2 texSize;
	F32 mipCount;
	g_momentsTex.GetDimensions(0, texSize.x, texSize.y, mipCount);
	const Vec2 texelSize = 1.0 / texSize;
	Vec2 sumMoments = 0.0f;

	for(I32 yy = -radius; yy <= radius; yy++)
	{
		for(I32 xx = -radius; xx <= radius; xx++)
		{
			const Vec2 newUv = uv + Vec2(xx, yy) * texelSize;
			const F32 k = kernel[abs(xx)][abs(yy)];
			sumMoments += g_momentsTex.SampleLevel(g_linearAnyClampSampler, newUv, 0.0).xy * k;
		}
	}

	return abs(sumMoments.y - sumMoments.x * sumMoments.x);
}

[numthreads(8, 8, 1)] void main(UVec3 svDispatchThreadId : SV_DISPATCHTHREADID)
{
	if(any(svDispatchThreadId.xy >= kOutImageSize))
	{
		return;
	}

	const Vec2 uv = (Vec2(svDispatchThreadId.xy) + 0.5) / Vec2(kOutImageSize);

	// Reference
	const F32 depthCenter = g_depthTex.SampleLevel(g_linearAnyClampSampler, uv, 0.0).r;
	if(depthCenter == 1.0)
	{
		g_outImg[svDispatchThreadId.xy] = 0;
		return;
	}

	const Vec3 positionCenter = unproject(uvToNdc(uv), depthCenter);
	const Vec3 normalCenter = unpackNormalFromGBuffer(g_gbuffer2Tex.SampleLevel(g_linearAnyClampSampler, uv, 0.0));

	RF32 shadowFactors[kMaxRtShadowLayers];
	unpackRtShadows(g_inTex.SampleLevel(g_nearestAnyClampSampler, uv, 0.0), shadowFactors);

	// Decide the amount of blurring
	const F32 varianceCenter = computeVarianceCenter(uv);
	const F32 historyLength =
		g_historyLengthTex.SampleLevel(g_linearAnyClampSampler, uv, 0.0).x * kRtShadowsMaxHistoryLength;

	U32 sampleCount;
	if(historyLength < 2.0)
	{
		// Worst case
		sampleCount = kMaxSampleCount;
	}
	else if(historyLength > 4.0 && varianceCenter < 0.0001)
	{
		// Best case
		sampleCount = kMinSampleCount;
	}
	else
	{
		// Every other case

		F32 blur = varianceCenter * 100.0;
		blur = min(1.0, blur);

		const F32 sampleCountf = lerp(F32(kMinSampleCount), F32(kMaxSampleCount), blur);

		sampleCount = U32(sampleCountf);
	}

	sampleCount = sampleCount / 2u;

	// Sample
	F32 weight = 1.0; // TODO fix that

	for(I32 i = -I32(sampleCount); i < I32(sampleCount); ++i)
	{
		const Vec2 texelSize = 1.0 / Vec2(kOutImageSize);
#if BLUR_ORIENTATION == 0
		const Vec2 sampleUv = Vec2(uv.x + F32(i) * texelSize.x, uv.y);
#else
		const Vec2 sampleUv = Vec2(uv.x, uv.y + F32(i) * texelSize.y);
#endif

		RF32 localShadowFactors[kMaxRtShadowLayers];
		unpackRtShadows(g_inTex.SampleLevel(g_nearestAnyClampSampler, sampleUv, 0.0), localShadowFactors);

		const F32 depthTap = g_depthTex.SampleLevel(g_linearAnyClampSampler, sampleUv, 0.0).r;
		const Vec3 positionTap = unproject(uvToNdc(sampleUv), depthTap);
		const Vec3 normalTap =
			unpackNormalFromGBuffer(g_gbuffer2Tex.SampleLevel(g_linearAnyClampSampler, sampleUv, 0.0));

		// F32 w = calculateBilateralWeighPlane(depthCenter, depthTap, 1.0);
		F32 w = calculateBilateralWeightPlane(positionCenter, normalCenter, positionTap, normalTap, 1.0);

		w *= gaussianWeight(0.4, abs(F32(i)) / F32(sampleCount + 1u));

		[unroll] for(U32 i = 0u; i < kMaxRtShadowLayers; ++i)
		{
			shadowFactors[i] += localShadowFactors[i] * w;
		}

		weight += w;
	}

	// Write value
	[unroll] for(U32 i = 0u; i < kMaxRtShadowLayers; ++i)
	{
		shadowFactors[i] /= weight;
	}

	g_outImg[svDispatchThreadId.xy] = packRtShadows(shadowFactors);
}

#pragma anki end
