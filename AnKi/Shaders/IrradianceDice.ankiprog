// Copyright (C) 2009-2023, Panagiotis Christopoulos Charitos and contributors.
// All rights reserved.
// Code licensed under the BSD License.
// http://www.anki3d.org/LICENSE

// Compute the irradiance given a light shading result. The irradiance will be stored in an ambient dice.

#pragma anki mutator WORKGROUP_SIZE_XY 8 16 32
#pragma anki mutator LIGHT_SHADING_TEX 0 1 // 0: texture2D, 1: textureCubeArray
#pragma anki mutator STORE_LOCATION 0 1 // 0: in a 3D texture, 1: In an SSBO
#pragma anki mutator SECOND_BOUNCE 0 1

#pragma anki start comp

#include <AnKi/Shaders/Functions.hlsl>
#include <AnKi/Shaders/PackFunctions.hlsl>
#include <AnKi/Shaders/LightFunctions.hlsl>

#define DEBUG_MODE 0 // 0: disable, 1: different color per dice face, 2: different color per cell

constexpr U32 kWorkgroupSize = U32(WORKGROUP_SIZE_XY) * U32(WORKGROUP_SIZE_XY);

[[vk::binding(0)]] SamplerState g_nearestAnyClampSampler;
#if LIGHT_SHADING_TEX == 0
[[vk::binding(1)]] Texture2D g_lightShadingTex2d;
#else
[[vk::binding(1)]] TextureCube<RVec4> g_lightShadingTexCube;
#endif

#if SECOND_BOUNCE == 1
[[vk::binding(2)]] Texture2D g_gbufferTex[3u];
#endif

#if STORE_LOCATION == 0
[[vk::binding(3)]] RWTexture3D<Vec4> g_irradianceVolume;

struct Uniforms
{
	IVec3 m_volumeTexel;
	I32 m_nextTexelOffsetInU;
};

[[vk::push_constant]] ConstantBuffer<Uniforms> g_uniforms;
#else
struct BufferOut
{
	RVec4 m_val[6u];
};

[[vk::binding(3)]] RWStructuredBuffer<BufferOut> g_irradianceDisceResults;
#endif

constexpr U32 kMinWaveSize = 8u;
groupshared Vec3 s_integrationResults[6u][kWorkgroupSize / kMinWaveSize];
groupshared U32 s_waveIndexInsideThreadGroup;

RVec3 sampleLightShadingTexture(const U32 face, UVec3 svGroupThreadId)
{
#if LIGHT_SHADING_TEX == 0
	const Vec2 kInputTexSize = Vec2(U32(WORKGROUP_SIZE_XY) * 6u, U32(WORKGROUP_SIZE_XY));
	const Vec2 uv = (Vec2(svGroupThreadId.x + U32(WORKGROUP_SIZE_XY) * face, svGroupThreadId.y) + 0.5) / kInputTexSize;

	return g_lightShadingTex2d.SampleLevel(g_nearestAnyClampSampler, uv, 0.0).rgb;
#else
	const Vec2 uv = (Vec2(svGroupThreadId.x, svGroupThreadId.y) + 0.5) / F32(WORKGROUP_SIZE_XY);
	const Vec2 ndc = uvToNdc(uv);
	const Vec3 cubeUvw = getCubemapDirection(ndc, face);

	return g_lightShadingTexCube.SampleLevel(g_nearestAnyClampSampler, cubeUvw, 0.0).rgb;
#endif
}

[numthreads(WORKGROUP_SIZE_XY, WORKGROUP_SIZE_XY, 1)] void main(UVec3 svGroupThreadId : SV_GROUPTHREADID,
																U32 svGroupIndex : SV_GROUPINDEX)
{
	U32 wavesPerThreadGroup;
	U32 waveIndexInsideThreadGroup;
	ANKI_COMPUTE_WAVE_INDEX_INSIDE_THREADGROUP(svGroupIndex, s_waveIndexInsideThreadGroup, waveIndexInsideThreadGroup,
											   wavesPerThreadGroup);
	const F32 workgroupSizeXYf = F32(WORKGROUP_SIZE_XY);

	// Compute the NDC used in cubeCoordSolidAngle
	const Vec2 faceUv = (Vec2(svGroupThreadId.xy) + 0.5) / workgroupSizeXYf;
	const Vec2 ndc = uvToNdc(faceUv);

	// Compute result for a pixel
	Vec3 resultFaces[6u];
	for(U32 f = 0u; f < 6u; ++f)
	{
		// Get the direction of the dice face
		const Vec3 diceDir = getCubemapDirection(Vec2(0.0, 0.0), f);

		const Vec3 r = getCubemapDirection(ndc, f);

		// Compute integral part
		const RF32 lambert = max(0.0, dot(r, diceDir));
		const RVec3 lightShading = sampleLightShadingTexture(f, svGroupThreadId);
		const RVec3 irradiance = lightShading * lambert * cubeCoordSolidAngle(ndc, workgroupSizeXYf);

		// Store
		resultFaces[f] = irradiance;
	}

	// Subgroup reduce
	[unroll] for(U32 f = 0u; f < 6u; ++f)
	{
		resultFaces[f] = WaveActiveSum(resultFaces[f]);
	}

	[branch] if(WaveIsFirstLane())
	{
		[unroll] for(U32 f = 0u; f < 6u; ++f)
		{
			s_integrationResults[f][waveIndexInsideThreadGroup] = resultFaces[f];
		}
	}

	GroupMemoryBarrierWithGroupSync();

	// Worgroup reduce
	[branch] if(svGroupIndex == 0u)
	{
		[unroll] for(U32 f = 0u; f < 6u; ++f)
		{
			for(U32 i = 1u; i < wavesPerThreadGroup; ++i)
			{
				s_integrationResults[f][0] += s_integrationResults[f][i];
			}
		}
	}

	GroupMemoryBarrierWithGroupSync();

#if SECOND_BOUNCE == 1
	// Initialize again for the 2nd bounce
	for(U32 f = 0u; f < 6u; ++f)
	{
		// Get the direction of the dice face
		const Vec3 diceDir = getCubemapDirection(Vec2(0.0, 0.0), f);

		const Vec3 r = getCubemapDirection(ndc, f);

		// Compute integral part
		const RF32 lambert = max(0.0, dot(r, diceDir));

		// Read the gbuffer
		Vec2 gbufferUv = (Vec2(svGroupThreadId.xy) + 0.5) / workgroupSizeXYf;
		gbufferUv.x *= 1.0 / 6.0;
		gbufferUv.x += (1.0 / 6.0) * F32(f);
		GbufferInfo gbuffer = (GbufferInfo)0;
		unpackGBufferNoVelocity(g_gbufferTex[0u].SampleLevel(g_nearestAnyClampSampler, gbufferUv, 0.0),
								g_gbufferTex[1u].SampleLevel(g_nearestAnyClampSampler, gbufferUv, 0.0),
								g_gbufferTex[2u].SampleLevel(g_nearestAnyClampSampler, gbufferUv, 0.0), gbuffer);

		// Sample irradiance
		RVec3 firstBounceIrradiance = sampleAmbientDice(
			s_integrationResults[0][0], s_integrationResults[1][0], s_integrationResults[2][0],
			s_integrationResults[3][0], s_integrationResults[4][0], s_integrationResults[5][0], gbuffer.m_normal);
		firstBounceIrradiance = gbuffer.m_diffuse * firstBounceIrradiance;

		// Compute 2nd bounce
		const RVec3 lightShading = sampleLightShadingTexture(f, svGroupThreadId);
		const RVec3 irradiance =
			(firstBounceIrradiance + lightShading * lambert) * cubeCoordSolidAngle(ndc, workgroupSizeXYf);

		// Store
		resultFaces[f] = irradiance;
	}

	// Subgroup reduce
	[unroll] for(U32 f = 0u; f < 6u; ++f)
	{
		resultFaces[f] = WaveActiveSum(resultFaces[f]);
	}

	[branch] if(WaveIsFirstLane())
	{
		[unroll] for(U32 f = 0u; f < 6u; ++f)
		{
			s_integrationResults[f][waveIndexInsideThreadGroup] = resultFaces[f];
		}
	}

	GroupMemoryBarrierWithGroupSync();

	// Worgroup reduce
	[branch] if(svGroupIndex == 0u)
	{
		[unroll] for(U32 f = 0u; f < 6u; ++f)
		{
			for(U32 i = 1u; i < wavesPerThreadGroup; ++i)
			{
				s_integrationResults[f][0] += s_integrationResults[f][i];
			}
		}
	}

	GroupMemoryBarrierWithGroupSync();
#endif

	// Store the results
	if(svGroupIndex < 6u)
	{
		const U32 f = svGroupIndex;

#if DEBUG_MODE == 0
		RVec3 irradiance = s_integrationResults[f][0];
		const RVec3 toStoreValue = irradiance;
#elif DEBUG_MODE == 1
		const RVec3 toStoreValue = colorPerCubeFace(f);
#else
		UVec3 volumeSize;
		g_irradianceVolume.GetDimensions(volumeSize.x, volumeSize.y, volumeSize.z);

		const UVec3 subvolumeSize = UVec3(volumeSize.x / 6u, volumeSize.y, volumeSize.z);
		const U32 cellIdx = g_uniforms.m_volumeTexel.z * subvolumeSize.x * subvolumeSize.y
							+ g_uniforms.m_volumeTexel.y * subvolumeSize.x + g_uniforms.m_volumeTexel.x;
		const RF32 headmapFactor = F32(cellIdx) / F32(subvolumeSize.x * subvolumeSize.y * subvolumeSize.z);
		const RVec3 toStoreValue = heatmap(headmapFactor);
#endif

#if STORE_LOCATION == 0
		const UVec3 storeUvw = UVec3(g_uniforms.m_volumeTexel.x + I32(f) * g_uniforms.m_nextTexelOffsetInU,
									 g_uniforms.m_volumeTexel.y, g_uniforms.m_volumeTexel.z);
		g_irradianceVolume[storeUvw] = Vec4(toStoreValue, 0.0);
#else
		g_irradianceDisceResults[0].m_val[f] = toStoreValue.xyzx;
#endif
	}
}

#pragma anki end
