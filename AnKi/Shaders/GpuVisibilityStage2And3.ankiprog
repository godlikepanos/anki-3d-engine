// Copyright (C) 2009-present, Panagiotis Christopoulos Charitos and contributors.
// All rights reserved.
// Code licensed under the BSD License.
// http://www.anki3d.org/LICENSE

#pragma anki mutator HZB_TEST 0 1
#pragma anki mutator PASSTHROUGH 0 1
#pragma anki mutator MESH_SHADERS 0 1
#pragma anki mutator STORE_MESHLETS_FAILED_HZB 0 1 // Two-phase occlusion culling

// This technique is used for legacy rendering. It gathers the visible renderables and places them into the correct buckets.
#pragma anki technique Legacy comp mutators

#pragma anki technique Meshlets comp mutators HZB_TEST PASSTHROUGH MESH_SHADERS STORE_MESHLETS_FAILED_HZB

#include <AnKi/Shaders/Include/GpuSceneTypes.h>
#include <AnKi/Shaders/Include/GpuVisibilityTypes.h>
#include <AnKi/Shaders/VisibilityAndCollisionFunctions.hlsl>
#include <AnKi/Shaders/PackFunctions.hlsl>
#include <AnKi/Shaders/Functions.hlsl>

#define NUMTHREADS 64u

// ===========================================================================
// Legacy                                                                    =
// ===========================================================================
#if ANKI_TECHNIQUE_Legacy
struct DrawIndirectArgsWithPadding
{
	U32 m_vertexCount;
	U32 m_instanceCount;
	U32 m_firstVertex;
	U32 m_firstInstance;
	U32 m_padding;
};

// GPU scene
StructuredBuffer<GpuSceneRenderable> g_renderables : register(t0);
StructuredBuffer<GpuSceneParticleEmitter2> g_particleEmitters2 : register(t1);
StructuredBuffer<GpuSceneMeshLod> g_meshLods : register(t2);

StructuredBuffer<GpuVisibilityVisibleRenderableDesc> g_visibleRenderables : register(t3);
StructuredBuffer<U32> g_counters : register(t4);
StructuredBuffer<U32> g_renderablePrefixSums : register(t5);

// One for each bucket. Points to the 1st indirect args struct. 2nd element contains the max count
StructuredBuffer<UVec2> g_firstDrawIndirectArgAndCount : register(t6);

// These 3 have the same size
RWStructuredBuffer<GpuScenePerDraw> g_perDraw : register(u0);
RWStructuredBuffer<DrawIndexedIndirectArgs> g_drawIndexedIndirectArgs : register(u1);

// The MDI counts. One for each render state bucket
RWStructuredBuffer<U32> g_mdiDrawCounts : register(u2);

RWStructuredBuffer<U32> g_outOfMemoryBuffer : register(u3);

[numthreads(NUMTHREADS, 1, 1)] void main(UVec3 svDispatchThreadId : SV_DISPATCHTHREADID)
{
	if(svDispatchThreadId.x >= g_counters[(U32)GpuVisibilityCounter::kVisibleRenderableCount])
	{
		return;
	}

	const GpuVisibilityVisibleRenderableDesc desc = SBUFF(g_visibleRenderables, svDispatchThreadId.x);

	const U32 renderStateBucket = desc.m_renderStateBucket;
	const U32 renderableIdx = desc.m_renderableIndex;
	const U32 lod = desc.m_lod;

	const GpuSceneRenderable renderable = SBUFF(g_renderables, renderableIdx);
	const U32 meshLodIndex = renderable.m_meshLodsIndex + lod;
	const GpuSceneMeshLod meshLod = SBUFF(g_meshLods, meshLodIndex);

	const Bool isParticleEmitter = renderable.m_particleEmitterIndex2 < kMaxU32;

	U32 bucketLocalIndex;
	InterlockedAdd(SBUFF(g_mdiDrawCounts, renderStateBucket), 1u, bucketLocalIndex);

	const U32 instanceIndex = bucketLocalIndex + SBUFF(g_renderablePrefixSums, renderStateBucket);
	const U32 indirectArgsIndex = bucketLocalIndex + SBUFF(g_firstDrawIndirectArgAndCount, renderStateBucket).x;

	if(bucketLocalIndex >= SBUFF(g_firstDrawIndirectArgAndCount, renderStateBucket).y)
	{
		// OoM, try to recover and inform the CPU
		U32 orig;
		InterlockedExchange(SBUFF(g_mdiDrawCounts, renderStateBucket), SBUFF(g_firstDrawIndirectArgAndCount, renderStateBucket).y, orig);
		InterlockedOr(SBUFF(g_outOfMemoryBuffer, 0), 2);
	}
	else
	{
		DrawIndexedIndirectArgs indirect;
		indirect.m_indexCount = meshLod.m_indexCount;
		indirect.m_firstIndex = meshLod.m_firstIndex;
		indirect.m_vertexOffset = 0;
		indirect.m_firstInstance = 0;

		if(isParticleEmitter)
		{
			const GpuSceneParticleEmitter2 emitter = SBUFF(g_particleEmitters2, renderable.m_particleEmitterIndex2);
			indirect.m_instanceCount = emitter.m_aliveParticleCount;
		}
		else
		{
			indirect.m_instanceCount = 1;
		}

		SBUFF(g_drawIndexedIndirectArgs, indirectArgsIndex) = indirect;

		GpuScenePerDraw perDraw;
		perDraw.m_worldTransformsIndex = renderable.m_worldTransformsIndex;
		perDraw.m_isParticleEmitter = isParticleEmitter;
		perDraw.m_particleEmitterIndex = renderable.m_particleEmitterIndex2;
		perDraw.m_constantsOffset = renderable.m_constantsOffset;
		perDraw.m_meshLodIndex = meshLodIndex;
		perDraw.m_boneTransformsOffset = renderable.m_boneTransformsOffset;
		SBUFF(g_perDraw, instanceIndex) = perDraw;
	}
}

#endif // ANKI_TECHNIQUE_Legacy

// ===========================================================================
// Meshlets                                                                  =
// ===========================================================================
#if ANKI_TECHNIQUE_Meshlets

#	define MESHLET_BACKFACE_CULLING 1 // Doesn't cull much geometry
#	define MESHLET_OUTSIDE_OF_SCREEN_CULLING 1
#	define MESHLET_NO_SAMPLING_POINT_CULLING 1
#	define MESHLET_HZB_CULLING HZB_TEST

// GPU scene
StructuredBuffer<GpuSceneRenderable> g_renderables : register(t0);
StructuredBuffer<GpuSceneMeshLod> g_meshLods : register(t1);
StructuredBuffer<Mat3x4> g_transforms : register(t2);

// UGB
StructuredBuffer<MeshletBoundingVolume> g_meshletBoundingVolumes : register(t3);

#	if MESHLET_HZB_CULLING
Texture2D<Vec4> g_hzbTexture : register(t4);
SamplerState g_nearestClampSampler : register(s0);
#	endif

// Prev stage results
RWStructuredBuffer<U32> g_counters : register(u0); // 2nd element is the visible meshlet count
StructuredBuffer<U32> g_meshletPrefixSums : register(t5);
StructuredBuffer<GpuVisibilityVisibleMeshletDesc> g_visibleMeshlets : register(t6);

// New results
#	if MESH_SHADERS
RWStructuredBuffer<DispatchIndirectArgs> g_dispatchMeshIndirectArgs : register(u1);
#	else
RWStructuredBuffer<DrawIndirectArgs> g_indirectDrawArgs : register(u1);
#	endif
RWStructuredBuffer<GpuSceneMeshletInstance> g_meshletInstances : register(u2);

RWStructuredBuffer<U32> g_outOfMemoryBuffer : register(u3);

#	if STORE_MESHLETS_FAILED_HZB
RWStructuredBuffer<GpuVisibilityVisibleMeshletDesc> g_meshletsFailedHzb : register(u4);

RWStructuredBuffer<DispatchIndirectArgs> g_gpuVisIndirectDispatchArgs : register(u5);
#	endif

ANKI_FAST_CONSTANTS(GpuVisibilityMeshletConstants, g_consts)

Bool cullMeshlet(GpuSceneRenderable renderable, const MeshletBoundingVolume meshletBoundingVol, out Bool meshletCulledByHzb)
{
	meshletCulledByHzb = false;

	const Bool bSkin = renderable.m_boneTransformsOffset != 0u;
	if(PASSTHROUGH || bSkin)
	{
		return false;
	}

	const Mat3x4 worldTransform = SBUFF(g_transforms, renderable.m_worldTransformsIndex);

	const Mat4 wordTransform4 = {worldTransform.m_row0, worldTransform.m_row1, worldTransform.m_row2, Vec4(0.0f, 0.0f, 0.0f, 1.0f)};
	const Mat4 mvp = mul(g_consts.m_viewProjectionMatrix, wordTransform4);

	Vec2 minNdc, maxNdc;
	F32 aabbMinDepth;
	projectAabb(meshletBoundingVol.m_aabbMin, meshletBoundingVol.m_aabbMax, mvp, minNdc, maxNdc, aabbMinDepth);

#	if MESHLET_OUTSIDE_OF_SCREEN_CULLING
	// Outside of the screen
	if(any(minNdc > 1.0f) || any(maxNdc < -1.0f))
	{
		return true;
	}
#	endif

#	if MESHLET_NO_SAMPLING_POINT_CULLING
	// Sampling points test
	const Vec2 windowCoordsMin = ndcToUv(minNdc) * g_consts.m_viewportSizef;
	const Vec2 windowCoordsMax = ndcToUv(maxNdc) * g_consts.m_viewportSizef;
	if(any(round(windowCoordsMin) == round(windowCoordsMax)))
	{
		return true;
	}
#	endif

#	if MESHLET_BACKFACE_CULLING
	{
		Vec3 center = (meshletBoundingVol.m_aabbMin + meshletBoundingVol.m_aabbMax) * 0.5;
		center = mul(worldTransform, Vec4(center, 1.0));

		const Vec4 coneDirAndAng = unpackSnorm4x8<F32>(meshletBoundingVol.m_coneDirection_R8G8B8_Snorm_cosHalfAngle_R8_Snorm);
		Vec3 coneDir = coneDirAndAng.xyz;
		coneDir = mul(worldTransform, Vec4(coneDir, 0.0));

		const Vec3 scale = extractScale(worldTransform);
		const F32 radius = meshletBoundingVol.m_sphereRadius * max3(scale);

		// Using from meshoptimizer: dot(center - camera_position, cone_axis) >= cone_cutoff * length(center - camera_position) + radius
		const Vec3 camPos = g_consts.m_cameraPos;
		const Vec3 cam2center = center - camPos;
		if(dot(cam2center, coneDir) >= coneDirAndAng.w * length(cam2center) + radius)
		{
			return true;
		}
	}
#	endif

#	if MESHLET_HZB_CULLING
	meshletCulledByHzb = cullHzb(minNdc, maxNdc, aabbMinDepth, g_hzbTexture, g_nearestClampSampler);
	return meshletCulledByHzb;
#	endif

	return false;
}

[numthreads(NUMTHREADS, 1, 1)] void main(U32 svDispatchThreadId : SV_DISPATCHTHREADID, U32 svGroupIndex : SV_GROUPINDEX)
{
	const U32 meshletsSurvivingStage1Count = SBUFF(g_counters, (U32)GpuVisibilityCounter::kMeshletsSurvivingStage1Count);
	if(svDispatchThreadId < meshletsSurvivingStage1Count)
	{
		const GpuVisibilityVisibleMeshletDesc desc = SBUFF(g_visibleMeshlets, svDispatchThreadId);

		const U32 renderableIdx = desc.m_renderableIndex;
		const U32 renderStateBucket = desc.m_renderStateBucket;
		const U32 lod = desc.m_lod;
		const U32 meshletIdx = desc.m_meshletIndex;

		const GpuSceneRenderable renderable = SBUFF(g_renderables, renderableIdx);
		const GpuSceneMeshLod meshLod = SBUFF(g_meshLods, renderable.m_meshLodsIndex + lod);
		const MeshletBoundingVolume meshletBoundingVol = SBUFF(g_meshletBoundingVolumes, meshLod.m_firstMeshletBoundingVolume + meshletIdx);

		// Meshlet culling
		Bool meshletCulledByHzb;
		const Bool cull = cullMeshlet(renderable, meshletBoundingVol, meshletCulledByHzb);

		if(!cull)
		{
			U32 instanceIdx;
#	if MESH_SHADERS
			InterlockedAdd(SBUFF(g_dispatchMeshIndirectArgs, renderStateBucket).m_threadGroupCountX, 1u, instanceIdx);
#	else
			InterlockedAdd(SBUFF(g_indirectDrawArgs, renderStateBucket).m_instanceCount, 1u, instanceIdx);
#	endif

			if(instanceIdx == 0)
			{
				// First instance, init the drawcall
#	if MESH_SHADERS
				SBUFF(g_dispatchMeshIndirectArgs, renderStateBucket).m_threadGroupCountY = 1u;
				SBUFF(g_dispatchMeshIndirectArgs, renderStateBucket).m_threadGroupCountZ = 1u;
#	else
				SBUFF(g_indirectDrawArgs, renderStateBucket).m_firstInstance = SBUFF(g_meshletPrefixSums, renderStateBucket);
#	endif
			}

#	if !MESH_SHADERS
			// Try to limit the vertex size
			InterlockedMax(SBUFF(g_indirectDrawArgs, renderStateBucket).m_vertexCount, meshletBoundingVol.m_primitiveCount * 3u);
#	endif

			GpuSceneMeshletInstance instance;
			instance.m_worldTransformsIndex_25bit_meshletPrimitiveCount_7bit = renderable.m_worldTransformsIndex << 7u;
			instance.m_worldTransformsIndex_25bit_meshletPrimitiveCount_7bit |= meshletBoundingVol.m_primitiveCount;
			instance.m_constantsOffset = renderable.m_constantsOffset;
			instance.m_boneTransformsOffsetOrParticleEmitterIndex = renderable.m_boneTransformsOffset;
			instance.m_meshletGeometryDescriptorIndex = meshLod.m_firstMeshletGeometryDescriptor + meshletIdx;

			SBUFF(g_meshletInstances, SBUFF(g_meshletPrefixSums, renderStateBucket) + instanceIdx) = instance;
		}

#	if STORE_MESHLETS_FAILED_HZB
		if(cull && meshletCulledByHzb)
		{
			U32 idx;
			InterlockedAdd(SBUFF(g_counters, (U32)GpuVisibilityCounter::kMeshletsCulledByHzbCount), 1u, idx);
			SBUFF(g_meshletsFailedHzb, idx) = desc;
		}
#	endif
	}

#	if STORE_MESHLETS_FAILED_HZB
	// Sync to make sure all the atomic ops have finished before the following code reads them
	AllMemoryBarrierWithGroupSync();

	// Check if it's the last threadgroup running
	if(svGroupIndex == 0)
	{
		U32 threadgroupIdx;
		InterlockedAdd(SBUFF(g_counters, (U32)GpuVisibilityCounter::kThreadgroupCount), 1, threadgroupIdx);
		const U32 threadgroupCount = (meshletsSurvivingStage1Count + NUMTHREADS - 1) / NUMTHREADS;
		const Bool lastThreadExecuting = (threadgroupIdx + 1 == threadgroupCount);

		if(lastThreadExecuting)
		{
			// Last thing executing, prepare stage 3

			const U32 meshletsNeedReTestingCount = SBUFF(g_counters, (U32)GpuVisibilityCounter::kMeshletsCulledByHzbCount);

			DispatchIndirectArgs args;
			args.m_threadGroupCountX = (meshletsNeedReTestingCount + NUMTHREADS - 1) / NUMTHREADS;
			args.m_threadGroupCountY = 1;
			args.m_threadGroupCountZ = 1;
			SBUFF(g_gpuVisIndirectDispatchArgs, (U32)GpuVisibilityIndirectDispatches::k3rdStageMeshlets) = args;

			SBUFF(g_counters, (U32)GpuVisibilityCounter::kMeshletsSurvivingStage1Count) = meshletsNeedReTestingCount;
		}
	}
#	endif
}
#endif // ANKI_TECHNIQUE_Meshlets
