// Copyright (C) 2009-2023, Panagiotis Christopoulos Charitos and contributors.
// All rights reserved.
// Code licensed under the BSD License.
// http://www.anki3d.org/LICENSE

#pragma anki mutator USE_SHADOW_LAYERS 0 1

#pragma anki start vert
#include <AnKi/Shaders/QuadVert.hlsl>
#pragma anki end

#pragma anki start frag
#include <AnKi/Shaders/PackFunctions.hlsl>
#include <AnKi/Shaders/Functions.hlsl>
#include <AnKi/Shaders/RtShadows.hlsl>

ANKI_SPECIALIZATION_CONSTANT_UVEC2(kTileCount, 0u);
ANKI_SPECIALIZATION_CONSTANT_U32(kZSplitCount, 2u);
ANKI_SPECIALIZATION_CONSTANT_U32(kTileSize, 3u);

#define CLUSTERED_SHADING_SET 0u
#define CLUSTERED_SHADING_UNIFORMS_BINDING 0u
#define CLUSTERED_SHADING_LIGHTS_BINDING 1u
#define CLUSTERED_SHADING_CLUSTERS_BINDING 4u
#include <AnKi/Shaders/ClusteredShadingCommon.hlsl>

[[vk::binding(5)]] SamplerState g_nearestAnyClampSampler;
[[vk::binding(6)]] SamplerState g_trilinearClampSampler;

[[vk::binding(7)]] Texture2D<RVec4> g_gbuffer0Tex;
[[vk::binding(8)]] Texture2D<RVec4> g_gbuffer1Tex;
[[vk::binding(9)]] Texture2D<RVec4> g_gbuffer2Tex;
[[vk::binding(10)]] Texture2D g_depthTex;
#if USE_SHADOW_LAYERS
[[vk::binding(11)]] Texture2D<UVec4> g_shadowLayersTex;
#else
[[vk::binding(12)]] Texture2D<RVec4> g_resolvedShadowsTex;
#endif

// Common code for lighting
#define LIGHTING_COMMON_BRDF() \
	const RVec3 frag2Light = light.m_position - worldPos; \
	const RVec3 l = normalize(frag2Light); \
	const RVec3 specC = specularIsotropicLobe(gbuffer, viewDir, l); \
	const RVec3 diffC = diffuseLobe(gbuffer.m_diffuse); \
	const RF32 att = computeAttenuationFactor(light.m_squareRadiusOverOne, frag2Light); \
	RF32 lambert = max(0.0, dot(gbuffer.m_normal, l));

RVec3 main(Vec4 svPosition : SV_POSITION, Vec2 uv : TEXCOORD) : SV_TARGET0
{
	const F32 depth = g_depthTex.SampleLevel(g_nearestAnyClampSampler, uv, 0.0).r;
	const Vec2 ndc = uvToNdc(uv);

	if(depth == 1.0)
	{
		return RVec3(0.0, 0.0, 0.0);
	}

	// Get world position
	const Vec4 worldPos4 = mul(g_clusteredShading.m_matrices.m_invertedViewProjectionJitter, Vec4(ndc, depth, 1.0));
	const Vec3 worldPos = worldPos4.xyz / worldPos4.w;

	// Get the cluster
	Cluster cluster = getClusterFragCoord(Vec3(svPosition.xy, depth), kTileSize, kTileCount, kZSplitCount,
										  g_clusteredShading.m_zSplitMagic.x, g_clusteredShading.m_zSplitMagic.y);

	// return clusterHeatmap(cluster, 1u << (U32)ClusteredObjectType::kPointLight);

	// Decode GBuffer
	GbufferInfo gbuffer = (GbufferInfo)0;
	unpackGBufferNoVelocity(g_gbuffer0Tex.SampleLevel(g_nearestAnyClampSampler, uv, 0.0),
							g_gbuffer1Tex.SampleLevel(g_nearestAnyClampSampler, uv, 0.0),
							g_gbuffer2Tex.SampleLevel(g_nearestAnyClampSampler, uv, 0.0), gbuffer);
	gbuffer.m_subsurface = max(gbuffer.m_subsurface, kSubsurfaceMin);

	// SM
#if USE_SHADOW_LAYERS
	RF32 resolvedSm[kMaxRtShadowLayers];
	unpackRtShadows(g_shadowLayersTex.SampleLevel(g_nearestAnyClampSampler, uv, 0.0), resolvedSm);
#else
	RVec4 resolvedSm = g_resolvedShadowsTex.SampleLevel(g_trilinearClampSampler, uv, 0.0);
	U32 resolvedSmIdx = 0u;
#endif

	// Ambient and emissive color
	RVec3 outColor = gbuffer.m_emission;

	// Dir light
	const RVec3 viewDir = normalize(g_clusteredShading.m_cameraPosition - worldPos);
	const DirectionalLight dirLight = g_clusteredShading.m_directionalLight;
	if(dirLight.m_active != 0u)
	{
		RF32 shadowFactor;
		if(dirLight.m_shadowCascadeCount > 0u)
		{
#if USE_SHADOW_LAYERS
			shadowFactor = resolvedSm[dirLight.m_shadowLayer];
#else
			shadowFactor = resolvedSm[0];
			++resolvedSmIdx;
#endif
		}
		else
		{
			shadowFactor = 1.0;
		}

		const RVec3 l = -dirLight.m_direction;

		const RF32 lambert = max(gbuffer.m_subsurface, dot(l, gbuffer.m_normal));

		const RVec3 diffC = diffuseLobe(gbuffer.m_diffuse);
		const RVec3 specC = specularIsotropicLobe(gbuffer, viewDir, l);

		outColor += (diffC + specC) * dirLight.m_diffuseColor * (shadowFactor * lambert);
	}

	// Point lights
	[loop] while(cluster.m_pointLightsMask != 0)
	{
		const I32 idx = firstbitlow2(cluster.m_pointLightsMask);
		cluster.m_pointLightsMask &= ~(ExtendedClusterObjectMask(1) << ExtendedClusterObjectMask(idx));
		const PointLight light = g_pointLights[idx];

		LIGHTING_COMMON_BRDF();

		[branch] if(light.m_shadowAtlasTileScale >= 0.0)
		{
#if USE_SHADOW_LAYERS
			const RF32 shadow = resolvedSm[light.m_shadowLayer];
#else
			const RF32 shadow = resolvedSm[resolvedSmIdx++];
#endif
			lambert *= shadow;
		}

		outColor += (diffC + specC) * light.m_diffuseColor * (att * max(gbuffer.m_subsurface, lambert));
	}

	// Spot lights
	[loop] while(cluster.m_spotLightsMask != 0)
	{
		const I32 idx = firstbitlow2(cluster.m_spotLightsMask);
		cluster.m_spotLightsMask &= ~(ExtendedClusterObjectMask(1) << ExtendedClusterObjectMask(idx));
		const SpotLight light = g_spotLights[idx];

		LIGHTING_COMMON_BRDF();

		const F32 spot = computeSpotFactor(l, light.m_outerCos, light.m_innerCos, light.m_direction);

		[branch] if(light.m_shadowLayer != kMaxU32)
		{
#if USE_SHADOW_LAYERS
			const RF32 shadow = resolvedSm[light.m_shadowLayer];
#else
			const RF32 shadow = resolvedSm[resolvedSmIdx++];
#endif
			lambert *= shadow;
		}

		outColor += (diffC + specC) * light.m_diffuseColor * (att * spot * max(gbuffer.m_subsurface, lambert));
	}

	outColor = min(outColor, RVec3(kMaxRF32, kMaxRF32, kMaxRF32));
	return outColor;
}
#pragma anki end
